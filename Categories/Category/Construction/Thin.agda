{-# OPTIONS --without-K --safe #-}

open import Relation.Binary using (Preorder)

-- a thin category is a category generated by preorder, ignoring the equivalence between morphisms. that is,
-- a thin category only considers the connectivity between two objects, not how they are connected.
module Categories.Category.Construction.Thin {o ℓ₁ ℓ₂} (P : Preorder o ℓ₁ ℓ₂) where

open import Level
open import Data.Unit using (⊤)
open import Function using (flip)

open import Categories.Category

open Preorder P

Thin : ∀ e → Category o ℓ₂ e
Thin e = record
  { Obj       = Carrier
  ; _⇒_       = _∼_
  ; _≈_       = λ _ _ → Lift e ⊤
  ; id        = refl
  ; _∘_       = flip trans
  ; assoc     = _
  ; identityˡ = _
  ; identityʳ = _
  ; equiv     = _
  ; ∘-resp-≈  = _
  }
