<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Categories.Functor.Bifunctor.Properties</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Symbol">{-#</a> <a id="5" class="Keyword">OPTIONS</a> <a id="13" class="Pragma">--without-K</a> <a id="25" class="Pragma">--safe</a> <a id="32" class="Symbol">#-}</a>

<a id="37" class="Keyword">module</a> <a id="44" href="Categories.Functor.Bifunctor.Properties.html" class="Module">Categories.Functor.Bifunctor.Properties</a> <a id="84" class="Keyword">where</a>

<a id="91" class="Keyword">open</a> <a id="96" class="Keyword">import</a> <a id="103" href="Level.html" class="Module">Level</a>
<a id="109" class="Keyword">open</a> <a id="114" class="Keyword">import</a> <a id="121" href="Data.Product.html" class="Module">Data.Product</a> <a id="134" class="Keyword">using</a> <a id="140" class="Symbol">(</a><a id="141" href="Agda.Builtin.Sigma.html#165" class="Record">Σ</a><a id="142" class="Symbol">;</a> <a id="144" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">_,_</a><a id="147" class="Symbol">)</a>

<a id="150" class="Keyword">open</a> <a id="155" class="Keyword">import</a> <a id="162" href="Categories.Category.html" class="Module">Categories.Category</a>
<a id="182" class="Keyword">open</a> <a id="187" class="Keyword">import</a> <a id="194" href="Categories.Category.Product.html" class="Module">Categories.Category.Product</a>
<a id="222" class="Keyword">open</a> <a id="227" class="Keyword">import</a> <a id="234" href="Categories.Functor.html" class="Module">Categories.Functor</a>
<a id="253" class="Keyword">open</a> <a id="258" class="Keyword">import</a> <a id="265" href="Categories.Functor.Bifunctor.html" class="Module">Categories.Functor.Bifunctor</a>
<a id="294" class="Keyword">open</a> <a id="299" class="Keyword">import</a> <a id="306" href="Categories.Functor.Construction.Constant.html" class="Module">Categories.Functor.Construction.Constant</a>
<a id="347" class="Keyword">open</a> <a id="352" class="Keyword">import</a> <a id="359" href="Categories.NaturalTransformation.html" class="Module">Categories.NaturalTransformation</a> <a id="392" class="Keyword">using</a> <a id="398" class="Symbol">(</a><a id="399" href="Categories.NaturalTransformation.Core.html#466" class="Record">NaturalTransformation</a><a id="420" class="Symbol">;</a> <a id="422" href="Categories.NaturalTransformation.Core.html#3439" class="Function Operator">_∘ˡ_</a><a id="426" class="Symbol">)</a> <a id="428" class="Keyword">renaming</a> <a id="437" class="Symbol">(</a><a id="438" href="Categories.NaturalTransformation.Core.html#2132" class="Function">id</a> <a id="441" class="Symbol">to</a> <a id="444" class="Function">idN</a><a id="447" class="Symbol">)</a>

<a id="450" class="Keyword">import</a> <a id="457" href="Categories.Morphism.Reasoning.html" class="Module">Categories.Morphism.Reasoning</a> <a id="487" class="Symbol">as</a> <a id="490" class="Module">MR</a>

<a id="494" class="Keyword">private</a>
  <a id="504" class="Keyword">variable</a>
    <a id="517" href="Categories.Functor.Bifunctor.Properties.html#517" class="Generalizable">o</a> <a id="519" href="Categories.Functor.Bifunctor.Properties.html#519" class="Generalizable">ℓ</a> <a id="521" href="Categories.Functor.Bifunctor.Properties.html#521" class="Generalizable">e</a> <a id="523" class="Symbol">:</a> <a id="525" href="Agda.Primitive.html#742" class="Postulate">Level</a>
    <a id="535" href="Categories.Functor.Bifunctor.Properties.html#535" class="Generalizable">C</a> <a id="537" href="Categories.Functor.Bifunctor.Properties.html#537" class="Generalizable">D</a> <a id="539" href="Categories.Functor.Bifunctor.Properties.html#539" class="Generalizable">E</a> <a id="541" class="Symbol">:</a> <a id="543" href="Categories.Category.Core.html#442" class="Record">Category</a> <a id="552" href="Categories.Functor.Bifunctor.Properties.html#517" class="Generalizable">o</a> <a id="554" href="Categories.Functor.Bifunctor.Properties.html#519" class="Generalizable">ℓ</a> <a id="556" href="Categories.Functor.Bifunctor.Properties.html#521" class="Generalizable">e</a>

<a id="559" class="Keyword">module</a> <a id="566" href="Categories.Functor.Bifunctor.Properties.html#566" class="Module">_</a> <a id="568" class="Symbol">(</a><a id="569" href="Categories.Functor.Bifunctor.Properties.html#569" class="Bound">F</a> <a id="571" class="Symbol">:</a> <a id="573" href="Categories.Functor.Bifunctor.html#442" class="Function">Bifunctor</a> <a id="583" href="Categories.Functor.Bifunctor.Properties.html#535" class="Generalizable">C</a> <a id="585" href="Categories.Functor.Bifunctor.Properties.html#537" class="Generalizable">D</a> <a id="587" href="Categories.Functor.Bifunctor.Properties.html#539" class="Generalizable">E</a><a id="588" class="Symbol">)</a> <a id="590" class="Keyword">where</a>
  <a id="598" class="Keyword">private</a>
    <a id="610" class="Keyword">module</a> <a id="617" href="Categories.Functor.Bifunctor.Properties.html#617" class="Module">C</a> <a id="619" class="Symbol">=</a> <a id="621" href="Categories.Category.Core.html#442" class="Module">Category</a> <a id="630" href="Categories.Functor.Bifunctor.Properties.html#583" class="Bound">C</a>
    <a id="636" class="Keyword">module</a> <a id="643" href="Categories.Functor.Bifunctor.Properties.html#643" class="Module">D</a> <a id="645" class="Symbol">=</a> <a id="647" href="Categories.Category.Core.html#442" class="Module">Category</a> <a id="656" href="Categories.Functor.Bifunctor.Properties.html#585" class="Bound">D</a>
    <a id="662" class="Keyword">module</a> <a id="669" href="Categories.Functor.Bifunctor.Properties.html#669" class="Module">E</a> <a id="671" class="Symbol">=</a> <a id="673" href="Categories.Category.Core.html#442" class="Module">Category</a> <a id="682" href="Categories.Functor.Bifunctor.Properties.html#587" class="Bound">E</a>

    <a id="689" class="Keyword">variable</a>
      <a id="704" href="Categories.Functor.Bifunctor.Properties.html#704" class="Generalizable">A</a> <a id="706" href="Categories.Functor.Bifunctor.Properties.html#706" class="Generalizable">B</a> <a id="708" class="Symbol">:</a> <a id="710" href="Categories.Category.Core.html#559" class="Function">C.Obj</a>
      <a id="722" href="Categories.Functor.Bifunctor.Properties.html#722" class="Generalizable">X</a> <a id="724" href="Categories.Functor.Bifunctor.Properties.html#724" class="Generalizable">Y</a> <a id="726" class="Symbol">:</a> <a id="728" href="Categories.Category.Core.html#559" class="Function">D.Obj</a>
      <a id="740" href="Categories.Functor.Bifunctor.Properties.html#740" class="Generalizable">f</a> <a id="742" href="Categories.Functor.Bifunctor.Properties.html#742" class="Generalizable">h</a> <a id="744" href="Categories.Functor.Bifunctor.Properties.html#744" class="Generalizable">j</a> <a id="746" class="Symbol">:</a> <a id="748" href="Categories.Functor.Bifunctor.Properties.html#704" class="Generalizable">A</a> <a id="750" href="Categories.Category.Core.html#575" class="Function Operator">C.⇒</a> <a id="754" href="Categories.Functor.Bifunctor.Properties.html#706" class="Generalizable">B</a>
      <a id="762" href="Categories.Functor.Bifunctor.Properties.html#762" class="Generalizable">g</a> <a id="764" href="Categories.Functor.Bifunctor.Properties.html#764" class="Generalizable">i</a> <a id="766" href="Categories.Functor.Bifunctor.Properties.html#766" class="Generalizable">k</a> <a id="768" class="Symbol">:</a> <a id="770" href="Categories.Functor.Bifunctor.Properties.html#722" class="Generalizable">X</a> <a id="772" href="Categories.Category.Core.html#575" class="Function Operator">D.⇒</a> <a id="776" href="Categories.Functor.Bifunctor.Properties.html#724" class="Generalizable">Y</a>

  <a id="781" class="Keyword">open</a> <a id="786" href="Categories.Functor.Bifunctor.Properties.html#669" class="Module">E</a>
  <a id="790" class="Keyword">open</a> <a id="795" href="Categories.Category.Core.html#2462" class="Module">HomReasoning</a>
  <a id="810" class="Keyword">open</a> <a id="815" href="Categories.Functor.Core.html#248" class="Module">Functor</a> <a id="823" href="Categories.Functor.Bifunctor.Properties.html#569" class="Bound">F</a>

  <a id="828" href="Categories.Functor.Bifunctor.Properties.html#828" class="Function Operator">[_]-commute</a> <a id="840" class="Symbol">:</a> <a id="842" href="Categories.Functor.Core.html#455" class="Field">F₁</a> <a id="845" class="Symbol">(</a><a id="846" href="Categories.Category.Core.html#630" class="Function">C.id</a> <a id="851" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="853" href="Categories.Functor.Bifunctor.Properties.html#762" class="Generalizable">g</a><a id="854" class="Symbol">)</a> <a id="856" href="Categories.Category.Core.html#656" class="Function Operator">∘</a> <a id="858" href="Categories.Functor.Core.html#455" class="Field">F₁</a> <a id="861" class="Symbol">(</a><a id="862" href="Categories.Functor.Bifunctor.Properties.html#740" class="Generalizable">f</a> <a id="864" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="866" href="Categories.Category.Core.html#630" class="Function">D.id</a><a id="870" class="Symbol">)</a> <a id="872" href="Categories.Category.Core.html#595" class="Function Operator">≈</a> <a id="874" href="Categories.Functor.Core.html#455" class="Field">F₁</a> <a id="877" class="Symbol">(</a><a id="878" href="Categories.Functor.Bifunctor.Properties.html#740" class="Generalizable">f</a> <a id="880" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="882" href="Categories.Category.Core.html#630" class="Function">D.id</a><a id="886" class="Symbol">)</a> <a id="888" href="Categories.Category.Core.html#656" class="Function Operator">∘</a> <a id="890" href="Categories.Functor.Core.html#455" class="Field">F₁</a> <a id="893" class="Symbol">(</a><a id="894" href="Categories.Category.Core.html#630" class="Function">C.id</a> <a id="899" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="901" href="Categories.Functor.Bifunctor.Properties.html#762" class="Generalizable">g</a><a id="902" class="Symbol">)</a>
  <a id="906" href="Categories.Functor.Bifunctor.Properties.html#828" class="Function Operator">[_]-commute</a> <a id="918" class="Symbol">{</a><a id="919" class="Argument">g</a> <a id="921" class="Symbol">=</a> <a id="923" href="Categories.Functor.Bifunctor.Properties.html#923" class="Bound">g</a><a id="924" class="Symbol">}</a> <a id="926" class="Symbol">{</a><a id="927" class="Argument">f</a> <a id="929" class="Symbol">=</a> <a id="931" href="Categories.Functor.Bifunctor.Properties.html#931" class="Bound">f</a><a id="932" class="Symbol">}</a> <a id="934" class="Symbol">=</a> <a id="936" href="Relation.Binary.Reasoning.Syntax.html#1510" class="Function Operator">begin</a>
    <a id="946" href="Categories.Functor.Core.html#455" class="Field">F₁</a> <a id="949" class="Symbol">(</a><a id="950" href="Categories.Category.Core.html#630" class="Function">C.id</a> <a id="955" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="957" href="Categories.Functor.Bifunctor.Properties.html#923" class="Bound">g</a><a id="958" class="Symbol">)</a> <a id="960" href="Categories.Category.Core.html#656" class="Function Operator">∘</a> <a id="962" href="Categories.Functor.Core.html#455" class="Field">F₁</a> <a id="965" class="Symbol">(</a><a id="966" href="Categories.Functor.Bifunctor.Properties.html#931" class="Bound">f</a> <a id="968" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="970" href="Categories.Category.Core.html#630" class="Function">D.id</a><a id="974" class="Symbol">)</a> <a id="976" href="Relation.Binary.Reasoning.Syntax.html#7400" class="Function">≈˘⟨</a> <a id="980" href="Categories.Functor.Core.html#565" class="Field">homomorphism</a> <a id="993" href="Relation.Binary.Reasoning.Syntax.html#7400" class="Function">⟩</a>
    <a id="999" href="Categories.Functor.Core.html#455" class="Field">F₁</a> <a id="1002" class="Symbol">(</a><a id="1003" href="Categories.Category.Core.html#630" class="Function">C.id</a> <a id="1008" href="Categories.Category.Core.html#656" class="Function Operator">C.∘</a> <a id="1012" href="Categories.Functor.Bifunctor.Properties.html#931" class="Bound">f</a> <a id="1014" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1016" href="Categories.Functor.Bifunctor.Properties.html#923" class="Bound">g</a> <a id="1018" href="Categories.Category.Core.html#656" class="Function Operator">D.∘</a> <a id="1022" href="Categories.Category.Core.html#630" class="Function">D.id</a><a id="1026" class="Symbol">)</a>  <a id="1029" href="Relation.Binary.Reasoning.Syntax.html#7049" class="Function">≈⟨</a> <a id="1032" href="Categories.Functor.Core.html#696" class="Field">F-resp-≈</a> <a id="1041" class="Symbol">(</a><a id="1042" href="Categories.Morphism.Reasoning.Core.html#1309" class="Function">MR.id-comm-sym</a> <a id="1057" href="Categories.Functor.Bifunctor.Properties.html#583" class="Bound">C</a> <a id="1059" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1061" href="Categories.Morphism.Reasoning.Core.html#1202" class="Function">MR.id-comm</a> <a id="1072" href="Categories.Functor.Bifunctor.Properties.html#585" class="Bound">D</a><a id="1073" class="Symbol">)</a> <a id="1075" href="Relation.Binary.Reasoning.Syntax.html#7049" class="Function">⟩</a>
    <a id="1081" href="Categories.Functor.Core.html#455" class="Field">F₁</a> <a id="1084" class="Symbol">(</a><a id="1085" href="Categories.Functor.Bifunctor.Properties.html#931" class="Bound">f</a> <a id="1087" href="Categories.Category.Core.html#656" class="Function Operator">C.∘</a> <a id="1091" href="Categories.Category.Core.html#630" class="Function">C.id</a> <a id="1096" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1098" href="Categories.Category.Core.html#630" class="Function">D.id</a> <a id="1103" href="Categories.Category.Core.html#656" class="Function Operator">D.∘</a> <a id="1107" href="Categories.Functor.Bifunctor.Properties.html#923" class="Bound">g</a><a id="1108" class="Symbol">)</a>  <a id="1111" href="Relation.Binary.Reasoning.Syntax.html#7049" class="Function">≈⟨</a> <a id="1114" href="Categories.Functor.Core.html#565" class="Field">homomorphism</a> <a id="1127" href="Relation.Binary.Reasoning.Syntax.html#7049" class="Function">⟩</a>
    <a id="1133" href="Categories.Functor.Core.html#455" class="Field">F₁</a> <a id="1136" class="Symbol">(</a><a id="1137" href="Categories.Functor.Bifunctor.Properties.html#931" class="Bound">f</a> <a id="1139" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1141" href="Categories.Category.Core.html#630" class="Function">D.id</a><a id="1145" class="Symbol">)</a> <a id="1147" href="Categories.Category.Core.html#656" class="Function Operator">∘</a> <a id="1149" href="Categories.Functor.Core.html#455" class="Field">F₁</a> <a id="1152" class="Symbol">(</a><a id="1153" href="Categories.Category.Core.html#630" class="Function">C.id</a> <a id="1158" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1160" href="Categories.Functor.Bifunctor.Properties.html#923" class="Bound">g</a><a id="1161" class="Symbol">)</a> <a id="1163" href="Relation.Binary.Reasoning.Syntax.html#12283" class="Function Operator">∎</a>

  <a id="1168" href="Categories.Functor.Bifunctor.Properties.html#1168" class="Function Operator">[_]-decompose₁</a> <a id="1183" class="Symbol">:</a> <a id="1185" href="Categories.Functor.Core.html#455" class="Field">F₁</a> <a id="1188" class="Symbol">(</a><a id="1189" href="Categories.Functor.Bifunctor.Properties.html#740" class="Generalizable">f</a> <a id="1191" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1193" href="Categories.Functor.Bifunctor.Properties.html#762" class="Generalizable">g</a><a id="1194" class="Symbol">)</a> <a id="1196" href="Categories.Category.Core.html#595" class="Function Operator">≈</a> <a id="1198" href="Categories.Functor.Core.html#455" class="Field">F₁</a> <a id="1201" class="Symbol">(</a><a id="1202" href="Categories.Functor.Bifunctor.Properties.html#740" class="Generalizable">f</a> <a id="1204" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1206" href="Categories.Category.Core.html#630" class="Function">D.id</a><a id="1210" class="Symbol">)</a> <a id="1212" href="Categories.Category.Core.html#656" class="Function Operator">∘</a> <a id="1214" href="Categories.Functor.Core.html#455" class="Field">F₁</a> <a id="1217" class="Symbol">(</a><a id="1218" href="Categories.Category.Core.html#630" class="Function">C.id</a> <a id="1223" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1225" href="Categories.Functor.Bifunctor.Properties.html#762" class="Generalizable">g</a><a id="1226" class="Symbol">)</a>
  <a id="1230" href="Categories.Functor.Bifunctor.Properties.html#1168" class="Function Operator">[_]-decompose₁</a> <a id="1245" class="Symbol">{</a><a id="1246" class="Argument">f</a> <a id="1248" class="Symbol">=</a> <a id="1250" href="Categories.Functor.Bifunctor.Properties.html#1250" class="Bound">f</a><a id="1251" class="Symbol">}</a> <a id="1253" class="Symbol">{</a><a id="1254" class="Argument">g</a> <a id="1256" class="Symbol">=</a> <a id="1258" href="Categories.Functor.Bifunctor.Properties.html#1258" class="Bound">g</a><a id="1259" class="Symbol">}</a> <a id="1261" class="Symbol">=</a> <a id="1263" href="Relation.Binary.Reasoning.Syntax.html#1510" class="Function Operator">begin</a>
    <a id="1273" href="Categories.Functor.Core.html#455" class="Field">F₁</a> <a id="1276" class="Symbol">(</a><a id="1277" href="Categories.Functor.Bifunctor.Properties.html#1250" class="Bound">f</a> <a id="1279" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1281" href="Categories.Functor.Bifunctor.Properties.html#1258" class="Bound">g</a><a id="1282" class="Symbol">)</a>                    <a id="1303" href="Relation.Binary.Reasoning.Syntax.html#7400" class="Function">≈˘⟨</a> <a id="1307" href="Categories.Functor.Core.html#696" class="Field">F-resp-≈</a> <a id="1316" class="Symbol">(</a><a id="1317" href="Categories.Category.Core.html#1145" class="Function">C.identityʳ</a> <a id="1329" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1331" href="Categories.Category.Core.html#1096" class="Function">D.identityˡ</a><a id="1342" class="Symbol">)</a> <a id="1344" href="Relation.Binary.Reasoning.Syntax.html#7400" class="Function">⟩</a>
    <a id="1350" href="Categories.Functor.Core.html#455" class="Field">F₁</a> <a id="1353" class="Symbol">(</a><a id="1354" href="Categories.Functor.Bifunctor.Properties.html#1250" class="Bound">f</a> <a id="1356" href="Categories.Category.Core.html#656" class="Function Operator">C.∘</a> <a id="1360" href="Categories.Category.Core.html#630" class="Function">C.id</a> <a id="1365" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1367" href="Categories.Category.Core.html#630" class="Function">D.id</a> <a id="1372" href="Categories.Category.Core.html#656" class="Function Operator">D.∘</a> <a id="1376" href="Categories.Functor.Bifunctor.Properties.html#1258" class="Bound">g</a><a id="1377" class="Symbol">)</a>  <a id="1380" href="Relation.Binary.Reasoning.Syntax.html#7049" class="Function">≈⟨</a> <a id="1383" href="Categories.Functor.Core.html#565" class="Field">homomorphism</a> <a id="1396" href="Relation.Binary.Reasoning.Syntax.html#7049" class="Function">⟩</a>
    <a id="1402" href="Categories.Functor.Core.html#455" class="Field">F₁</a> <a id="1405" class="Symbol">(</a><a id="1406" href="Categories.Functor.Bifunctor.Properties.html#1250" class="Bound">f</a> <a id="1408" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1410" href="Categories.Category.Core.html#630" class="Function">D.id</a><a id="1414" class="Symbol">)</a> <a id="1416" href="Categories.Category.Core.html#656" class="Function Operator">∘</a> <a id="1418" href="Categories.Functor.Core.html#455" class="Field">F₁</a> <a id="1421" class="Symbol">(</a><a id="1422" href="Categories.Category.Core.html#630" class="Function">C.id</a> <a id="1427" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1429" href="Categories.Functor.Bifunctor.Properties.html#1258" class="Bound">g</a><a id="1430" class="Symbol">)</a> <a id="1432" href="Relation.Binary.Reasoning.Syntax.html#12283" class="Function Operator">∎</a>

  <a id="1437" href="Categories.Functor.Bifunctor.Properties.html#1437" class="Function Operator">[_]-decompose₂</a> <a id="1452" class="Symbol">:</a> <a id="1454" href="Categories.Functor.Core.html#455" class="Field">F₁</a> <a id="1457" class="Symbol">(</a><a id="1458" href="Categories.Functor.Bifunctor.Properties.html#740" class="Generalizable">f</a> <a id="1460" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1462" href="Categories.Functor.Bifunctor.Properties.html#762" class="Generalizable">g</a><a id="1463" class="Symbol">)</a> <a id="1465" href="Categories.Category.Core.html#595" class="Function Operator">≈</a> <a id="1467" href="Categories.Functor.Core.html#455" class="Field">F₁</a> <a id="1470" class="Symbol">(</a><a id="1471" href="Categories.Category.Core.html#630" class="Function">C.id</a> <a id="1476" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1478" href="Categories.Functor.Bifunctor.Properties.html#762" class="Generalizable">g</a><a id="1479" class="Symbol">)</a> <a id="1481" href="Categories.Category.Core.html#656" class="Function Operator">∘</a> <a id="1483" href="Categories.Functor.Core.html#455" class="Field">F₁</a> <a id="1486" class="Symbol">(</a><a id="1487" href="Categories.Functor.Bifunctor.Properties.html#740" class="Generalizable">f</a> <a id="1489" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1491" href="Categories.Category.Core.html#630" class="Function">D.id</a><a id="1495" class="Symbol">)</a>
  <a id="1499" href="Categories.Functor.Bifunctor.Properties.html#1437" class="Function Operator">[_]-decompose₂</a> <a id="1514" class="Symbol">{</a><a id="1515" class="Argument">f</a> <a id="1517" class="Symbol">=</a> <a id="1519" href="Categories.Functor.Bifunctor.Properties.html#1519" class="Bound">f</a><a id="1520" class="Symbol">}</a> <a id="1522" class="Symbol">{</a><a id="1523" class="Argument">g</a> <a id="1525" class="Symbol">=</a> <a id="1527" href="Categories.Functor.Bifunctor.Properties.html#1527" class="Bound">g</a><a id="1528" class="Symbol">}</a> <a id="1530" class="Symbol">=</a> <a id="1532" href="Relation.Binary.Reasoning.Syntax.html#1510" class="Function Operator">begin</a>
    <a id="1542" href="Categories.Functor.Core.html#455" class="Field">F₁</a> <a id="1545" class="Symbol">(</a><a id="1546" href="Categories.Functor.Bifunctor.Properties.html#1519" class="Bound">f</a> <a id="1548" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1550" href="Categories.Functor.Bifunctor.Properties.html#1527" class="Bound">g</a><a id="1551" class="Symbol">)</a>                    <a id="1572" href="Relation.Binary.Reasoning.Syntax.html#7400" class="Function">≈˘⟨</a> <a id="1576" href="Categories.Functor.Core.html#696" class="Field">F-resp-≈</a> <a id="1585" class="Symbol">(</a><a id="1586" href="Categories.Category.Core.html#1096" class="Function">C.identityˡ</a> <a id="1598" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1600" href="Categories.Category.Core.html#1145" class="Function">D.identityʳ</a><a id="1611" class="Symbol">)</a> <a id="1613" href="Relation.Binary.Reasoning.Syntax.html#7400" class="Function">⟩</a>
    <a id="1619" href="Categories.Functor.Core.html#455" class="Field">F₁</a> <a id="1622" class="Symbol">(</a><a id="1623" href="Categories.Category.Core.html#630" class="Function">C.id</a> <a id="1628" href="Categories.Category.Core.html#656" class="Function Operator">C.∘</a> <a id="1632" href="Categories.Functor.Bifunctor.Properties.html#1519" class="Bound">f</a> <a id="1634" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1636" href="Categories.Functor.Bifunctor.Properties.html#1527" class="Bound">g</a> <a id="1638" href="Categories.Category.Core.html#656" class="Function Operator">D.∘</a> <a id="1642" href="Categories.Category.Core.html#630" class="Function">D.id</a><a id="1646" class="Symbol">)</a>  <a id="1649" href="Relation.Binary.Reasoning.Syntax.html#7049" class="Function">≈⟨</a> <a id="1652" href="Categories.Functor.Core.html#565" class="Field">homomorphism</a> <a id="1665" href="Relation.Binary.Reasoning.Syntax.html#7049" class="Function">⟩</a>
    <a id="1671" href="Categories.Functor.Core.html#455" class="Field">F₁</a> <a id="1674" class="Symbol">(</a><a id="1675" href="Categories.Category.Core.html#630" class="Function">C.id</a> <a id="1680" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1682" href="Categories.Functor.Bifunctor.Properties.html#1527" class="Bound">g</a><a id="1683" class="Symbol">)</a> <a id="1685" href="Categories.Category.Core.html#656" class="Function Operator">∘</a> <a id="1687" href="Categories.Functor.Core.html#455" class="Field">F₁</a> <a id="1690" class="Symbol">(</a><a id="1691" href="Categories.Functor.Bifunctor.Properties.html#1519" class="Bound">f</a> <a id="1693" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1695" href="Categories.Category.Core.html#630" class="Function">D.id</a><a id="1699" class="Symbol">)</a> <a id="1701" href="Relation.Binary.Reasoning.Syntax.html#12283" class="Function Operator">∎</a>

  <a id="1706" href="Categories.Functor.Bifunctor.Properties.html#1706" class="Function Operator">[_]-merge</a> <a id="1716" class="Symbol">:</a> <a id="1718" href="Categories.Functor.Bifunctor.Properties.html#740" class="Generalizable">f</a> <a id="1720" href="Categories.Category.Core.html#656" class="Function Operator">C.∘</a> <a id="1724" href="Categories.Functor.Bifunctor.Properties.html#742" class="Generalizable">h</a> <a id="1726" href="Categories.Category.Core.html#595" class="Function Operator">C.≈</a> <a id="1730" href="Categories.Functor.Bifunctor.Properties.html#744" class="Generalizable">j</a> <a id="1732" class="Symbol">→</a> <a id="1734" href="Categories.Functor.Bifunctor.Properties.html#762" class="Generalizable">g</a> <a id="1736" href="Categories.Category.Core.html#656" class="Function Operator">D.∘</a> <a id="1740" href="Categories.Functor.Bifunctor.Properties.html#764" class="Generalizable">i</a> <a id="1742" href="Categories.Category.Core.html#595" class="Function Operator">D.≈</a> <a id="1746" href="Categories.Functor.Bifunctor.Properties.html#766" class="Generalizable">k</a> <a id="1748" class="Symbol">→</a>  <a id="1751" href="Categories.Functor.Core.html#455" class="Field">F₁</a> <a id="1754" class="Symbol">(</a><a id="1755" href="Categories.Functor.Bifunctor.Properties.html#740" class="Generalizable">f</a> <a id="1757" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1759" href="Categories.Functor.Bifunctor.Properties.html#762" class="Generalizable">g</a><a id="1760" class="Symbol">)</a> <a id="1762" href="Categories.Category.Core.html#656" class="Function Operator">∘</a> <a id="1764" href="Categories.Functor.Core.html#455" class="Field">F₁</a> <a id="1767" class="Symbol">(</a><a id="1768" href="Categories.Functor.Bifunctor.Properties.html#742" class="Generalizable">h</a> <a id="1770" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1772" href="Categories.Functor.Bifunctor.Properties.html#764" class="Generalizable">i</a><a id="1773" class="Symbol">)</a> <a id="1775" href="Categories.Category.Core.html#595" class="Function Operator">≈</a> <a id="1777" href="Categories.Functor.Core.html#455" class="Field">F₁</a> <a id="1780" class="Symbol">(</a><a id="1781" href="Categories.Functor.Bifunctor.Properties.html#744" class="Generalizable">j</a> <a id="1783" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1785" href="Categories.Functor.Bifunctor.Properties.html#766" class="Generalizable">k</a><a id="1786" class="Symbol">)</a>
  <a id="1790" href="Categories.Functor.Bifunctor.Properties.html#1706" class="Function Operator">[_]-merge</a> <a id="1800" class="Symbol">{</a><a id="1801" class="Argument">f</a> <a id="1803" class="Symbol">=</a> <a id="1805" href="Categories.Functor.Bifunctor.Properties.html#1805" class="Bound">f</a><a id="1806" class="Symbol">}</a> <a id="1808" class="Symbol">{</a><a id="1809" class="Argument">h</a> <a id="1811" class="Symbol">=</a> <a id="1813" href="Categories.Functor.Bifunctor.Properties.html#1813" class="Bound">h</a><a id="1814" class="Symbol">}</a> <a id="1816" class="Symbol">{</a><a id="1817" class="Argument">j</a> <a id="1819" class="Symbol">=</a> <a id="1821" href="Categories.Functor.Bifunctor.Properties.html#1821" class="Bound">j</a><a id="1822" class="Symbol">}</a> <a id="1824" class="Symbol">{</a><a id="1825" class="Argument">g</a> <a id="1827" class="Symbol">=</a> <a id="1829" href="Categories.Functor.Bifunctor.Properties.html#1829" class="Bound">g</a><a id="1830" class="Symbol">}</a> <a id="1832" class="Symbol">{</a><a id="1833" class="Argument">i</a> <a id="1835" class="Symbol">=</a> <a id="1837" href="Categories.Functor.Bifunctor.Properties.html#1837" class="Bound">i</a><a id="1838" class="Symbol">}</a> <a id="1840" class="Symbol">{</a><a id="1841" class="Argument">k</a> <a id="1843" class="Symbol">=</a> <a id="1845" href="Categories.Functor.Bifunctor.Properties.html#1845" class="Bound">k</a><a id="1846" class="Symbol">}</a> <a id="1848" href="Categories.Functor.Bifunctor.Properties.html#1848" class="Bound">eq</a> <a id="1851" href="Categories.Functor.Bifunctor.Properties.html#1851" class="Bound">eq′</a> <a id="1855" class="Symbol">=</a> <a id="1857" href="Relation.Binary.Reasoning.Syntax.html#1510" class="Function Operator">begin</a>
    <a id="1867" href="Categories.Functor.Core.html#455" class="Field">F₁</a> <a id="1870" class="Symbol">(</a><a id="1871" href="Categories.Functor.Bifunctor.Properties.html#1805" class="Bound">f</a> <a id="1873" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1875" href="Categories.Functor.Bifunctor.Properties.html#1829" class="Bound">g</a><a id="1876" class="Symbol">)</a> <a id="1878" href="Categories.Category.Core.html#656" class="Function Operator">∘</a> <a id="1880" href="Categories.Functor.Core.html#455" class="Field">F₁</a> <a id="1883" class="Symbol">(</a><a id="1884" href="Categories.Functor.Bifunctor.Properties.html#1813" class="Bound">h</a> <a id="1886" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1888" href="Categories.Functor.Bifunctor.Properties.html#1837" class="Bound">i</a><a id="1889" class="Symbol">)</a> <a id="1891" href="Relation.Binary.Reasoning.Syntax.html#7400" class="Function">≈˘⟨</a> <a id="1895" href="Categories.Functor.Core.html#565" class="Field">homomorphism</a> <a id="1908" href="Relation.Binary.Reasoning.Syntax.html#7400" class="Function">⟩</a>
    <a id="1914" href="Categories.Functor.Core.html#455" class="Field">F₁</a> <a id="1917" class="Symbol">(</a><a id="1918" href="Categories.Functor.Bifunctor.Properties.html#1805" class="Bound">f</a> <a id="1920" href="Categories.Category.Core.html#656" class="Function Operator">C.∘</a> <a id="1924" href="Categories.Functor.Bifunctor.Properties.html#1813" class="Bound">h</a> <a id="1926" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1928" href="Categories.Functor.Bifunctor.Properties.html#1829" class="Bound">g</a> <a id="1930" href="Categories.Category.Core.html#656" class="Function Operator">D.∘</a> <a id="1934" href="Categories.Functor.Bifunctor.Properties.html#1837" class="Bound">i</a><a id="1935" class="Symbol">)</a>  <a id="1938" href="Relation.Binary.Reasoning.Syntax.html#7049" class="Function">≈⟨</a> <a id="1941" href="Categories.Functor.Core.html#696" class="Field">F-resp-≈</a> <a id="1950" class="Symbol">(</a><a id="1951" href="Categories.Functor.Bifunctor.Properties.html#1848" class="Bound">eq</a> <a id="1954" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1956" href="Categories.Functor.Bifunctor.Properties.html#1851" class="Bound">eq′</a><a id="1959" class="Symbol">)</a> <a id="1961" href="Relation.Binary.Reasoning.Syntax.html#7049" class="Function">⟩</a>
    <a id="1967" href="Categories.Functor.Core.html#455" class="Field">F₁</a> <a id="1970" class="Symbol">(</a><a id="1971" href="Categories.Functor.Bifunctor.Properties.html#1821" class="Bound">j</a> <a id="1973" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1975" href="Categories.Functor.Bifunctor.Properties.html#1845" class="Bound">k</a><a id="1976" class="Symbol">)</a>              <a id="1991" href="Relation.Binary.Reasoning.Syntax.html#12283" class="Function Operator">∎</a>
</pre></body></html>